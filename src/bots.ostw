/** Enemy bot management and ai. (Could split)
*/
import "pathfinding/pathfinding.ostw";

// Spawning bots is done from the perspective of the bot, it see how many bots are expected and spawns itself.
// Spawning of bots should be primarily handled by the gamemode.
// Could use the slot of the bot for addressing.

// I can have 19 dummy bots in total.
// I have 15 max unique skins with 5 reserved for players for a total of 10 for dummies.

// Should the friendly bot be defined and managed here?
globalvar Player gFriendlyBot;
globalvar Number gBotQuota;
playervar Player pTarget;
playervar Player pFollowing;
playervar Boolean pIsBoss;

void RespawnBot(in Player _dummy)
{
	# Cannot use resurrect as it breaks custom game health settings
	Respawn(_dummy);
	# Minimum time before teleport can function after calling respawn
	Wait(0.033);
	Teleport(_dummy);
}

rule: "[BOTS] Create all the dummy bots" -10
{
	# Create friendly bot
	gFriendlyBot = CreateDummyBot(
		Hero.Mauga,
		Team.Team1,
		6,
		Vector(0, 0, 0),
		null
	);
	gFriendlyBot.PF_pPlayerAttributes = [ZERO, ONE];
	StartForcingDummyBotName(gFriendlyBot, "Puipui");

	# Create enemy bots
	for (gIter = ZERO; gIter < 17; gIter++)
	{
		CreateDummyBot(
			Hero.Soldier76,
			Team.Team2,
			-1,
			Vector(0, -1000, 0),
			null
		).PF_pPlayerAttributes = [ZERO, ONE, 6, 10, 20];
	}
}

rule: "[BOTS] Following"
Event.OngoingPlayer
if (IS_DUMMY)
if (pFollowing != null)
# Horizontal distance to following position is more than 10
if (
	(PositionOf() * Vector(1, 0, 1))
		.DistanceTo(pFollowing.Position() * Vector(1, 0, 1)) > 10
)
{
	# Pause updates if server is being overloaded
	WaitUntil(ServerLoadAverage() < 230, 999);
	# Set the destination as the ground under the player.
	PF_pDestination = positionUnderPlayer(pFollowing);
	# Avoid spamming destination updates
	Wait(1);
	LoopIfConditionIsTrue();
}

rule: "[BOTS] Facing"
Event.OngoingPlayer
if (IS_DUMMY)
if (pTarget != null)
{
	StartFacing(
		EventPlayer(),
		DirectionTowards(EyePosition(), pTarget.EyePosition()),
		500,
		Relative.ToWorld,
		FacingRev.DirectionAndTurnRate
	);
}

rule: "[BOTS] Target LOS check"
Event.OngoingPlayer
if (IS_DUMMY)
if (pTarget != null)
if (
	IsInLineOfSight(
		EyePosition(),
		pTarget.EyePosition(),
		BarrierLOS.NoBarriersBlock
	) == false
)
{
	StopFacing();
	pTarget = null;
}

// TODO Replace
Player TEMP_firstPlayer()
{
	return AllPlayers(Team.Team1).Filter(p => !p.IsDummy()).First;
}

rule: "[BOTS] Friendly bot force revive player"
Event.OngoingPlayer
if (IS_DUMMY)
if (EventPlayer() == gFriendlyBot)
# Will need to be changed
if (TEMP_firstPlayer().pDownedStartTime)
{
	while (TEMP_firstPlayer().pDownedStartTime)
	{
		# Continuously update the destination every second to make sure the bot stays in the revive circle
		gFriendlyBot.PF_pDestination = TEMP_firstPlayer().Position();
		Wait(1);
	}
}
